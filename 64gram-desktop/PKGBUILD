# Maintainer: Alesar1
# Contributor: solopasha <daron439 at gmail dot com>
# Contributor: KspLite <ksplite@outlook.com>
# Contributor: Daniil Kovalev <daniil@kovalev.website>
pkgname=64gram-desktop
_pkgname=64Gram
pkgver=1.1.93
pkgrel=1
epoch=1
pkgdesc='Unofficial desktop version of Telegram messaging app'
arch=('x86_64')
url="https://github.com/TDesktop-x64/tdesktop"
license=('GPL3')
depends=('hunspell' 'ffmpeg' 'hicolor-icon-theme' 'lz4' 'minizip' 'openal'
         'qt6-imageformats' 'qt6-svg' 'qt6-wayland' 'xxhash' 'ada'
         'rnnoise' 'pipewire' 'libxtst' 'libxrandr' 'abseil-cpp' 'libdispatch'
         'openssl' 'protobuf' 'glib2' 'libsigc++-3.0'
         'libxcomposite' 'libvpx' 'libxdamage' 'kcoreaddons' 'jemalloc' 'openh264'
         'libavif' 'libheif' 'libjxl')
makedepends=('cmake' 'git' 'ninja' 'python' 'range-v3' 'tl-expected' 'microsoft-gsl' 'meson'
             'extra-cmake-modules' 'wayland-protocols' 'plasma-wayland-protocols' 'libtg_owt'
             'gobject-introspection' 'boost' 'fmt' 'mm-common' 'perl-xml-parser' 'python-packaging'
             'dos2unix' 'glib2-devel' 'gperf')
optdepends=('webkit2gtk: embedded browser features'
            'xdg-desktop-portal: desktop integration')
_td_commit_sha=51743dfd01dff6179e2d8f7095729caa4e2222e9
source=("https://github.com/TDesktop-x64/tdesktop/releases/download/v${pkgver}/${_pkgname}-${pkgver}-full.tar.gz"
        "git+https://github.com/tdlib/td.git#tag=${_td_commit_sha}"
        "block-sponsored_messages.patch"
        "fix-lzma-link.patch"
        "io.github.tdesktop_x64.TDesktop.desktop")

#noextract=("${_pkgname}-${pkgver}-full.tar.gz")

sha256sums=('fe47a67c83d3c624ef6b8a6c66150a73e075b11d1f031d52066470a548307600'
            'SKIP'
            'c23c99b1b11d1582df34897bd9b4b0e814f4e72148e8ea3733c0808a6c9f0c78'
            '7d337b57a1e3502bac64dd87eb5140cda8238938c30533e12f828f52151bbbcf'
            '693a07b4e59d9c2ab271f493d1a9bac8890d6c8174fe60fb5db7396a06ff1f96')

prepare() {
    #LANG=C.UTF-8 bsdtar -xf ${_pkgname}-${pkgver}-full.tar.gz
    cd $_pkgname-$pkgver-full
    #find "${srcdir}"/ -type f -exec dos2unix {} \;
    echo "trying to finding out broken symlinks"

    # 1. 查找所有小于 512 字节的文件（符号链接通常非常小）
    # 2. 读取文件内容作为“目标路径”
    # 3. 检查该“目标路径”是否存在
    # 4. 如果存在，则删除文本文件并建立真正的符号链接

    find . -type f -size -512c | while read -r file; do
            # 1. 跳过 .git 开头的文件（通常构建不需要，且容易干扰）
            if [[ "$(basename "$file")" == .git* ]]; then
                continue
            fi

            # 读取潜在的目标路径
            target=$(cat "$file")

            # 2. 【关键修复】如果内容为空，或者全是空白字符，直接跳过
            if [[ -z "${target// /}" ]]; then
                continue
            fi

            dir=$(dirname "$file")

            # 3. 检查目标是否存在
            # 注意：这里我们只处理原本设计为指向“文件”或“目录”的链接
            # 通过判定 "$dir/$target" 是否存在来避免死链
            if [[ -e "$dir/$target" ]]; then
                # 双重确认：避免把目录误判为同名文件（防止递归引用）
                if [[ "$(realpath "$dir/$target")" != "$(realpath "$file")" ]]; then
                    echo "fixed symlink: $file -> $target"
                    rm "$file"
                    ln -s "$target" "$file"
                fi
            fi
    done

    patch -Np1 --binary -i ${srcdir}/block-sponsored_messages.patch
    patch -Np1 --binary -i ${srcdir}/fix-lzma-link.patch
}

build() {
    CXXFLAGS+=' -ffat-lto-objects'

    cmake -S td -B td/build \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX="$PWD/td/install" \
        -Wno-dev \
        -DTD_E2E_ONLY=ON
    cmake --build td/build
    cmake --install td/build

    cmake \
        -B build \
        -S $_pkgname-$pkgver-full \
        -G Ninja \
        -DCMAKE_VERBOSE_MAKEFILE=ON \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -Dtde2e_DIR="$PWD/td/install/lib/cmake/tde2e" \
        -DCMAKE_BUILD_TYPE=Release \
        -DDESKTOP_APP_DISABLE_AUTOUPDATE=ON \
        -DTDESKTOP_API_TEST=ON
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    mv "$pkgdir/usr/bin/telegram-desktop" "$pkgdir/usr/bin/64gram-desktop"
    install -Dm644 "$srcdir/io.github.tdesktop_x64.TDesktop.desktop" -t "$pkgdir/usr/share/applications"
    find "$pkgdir" -type f -name "telegram.png" -exec rename telegram.png 64gram.png {} \;
    mv "$pkgdir/usr/share/icons/hicolor/symbolic/apps/telegram-symbolic.svg" "$pkgdir/usr/share/icons/hicolor/symbolic/apps/64gram-symbolic.svg"
    mkdir -p "$pkgdir/usr/share/64Gram/externalupdater.d"
    echo "/usr/bin/64gram-desktop" >"$pkgdir/usr/share/64Gram/externalupdater.d/telegram-desktop.conf"
}